programa parcial7
procesos
  proceso juntarFlores(ES f:numero)
  comenzar
    mientras HayFlorEnLaEsquina
      tomarFlor
      f:=f+1
  fin
  proceso lado(ES f:numero)
  comenzar
    repetir 5
      juntarFlores(f)
      mover
    juntarFlores(f)
    derecha
  fin
  proceso continuar
  comenzar
    EnviarMensaje(V, r1)
    EnviarMensaje(V, r2)
    EnviarMensaje(V, r3)
  fin
  proceso avisarGanador(E id:numero)
  comenzar
    si(id=1)
      EnviarMensaje(V, r1)
      EnviarMensaje(F, r2)
      EnviarMensaje(F, r3)
    sino
      si(id=2)
        EnviarMensaje(F, r1)
        EnviarMensaje(V, r2)
        EnviarMensaje(F, r3)
      sino
        EnviarMensaje(F, r1)
        EnviarMensaje(F, r2)
        EnviarMensaje(V, r3)
  fin
  proceso identificar
  comenzar
    EnviarMensaje(1,r1)
    EnviarMensaje(2,r2)
    EnviarMensaje(3,r3)
  fin
  proceso maximo(E id:numero;E f:numero;ES idm:numero;ES fm:numero)
  comenzar
    si (f>fm)
      idm:=id
      fm:=f
  fin
  proceso depositarFlores
  variables
    miAv,miCa:numero
  comenzar
    miAv:=PosAv
    miCa:=PosCa
    BloquearEsquina(10,10)
    Pos(10,10)
    depositarFlor
    Pos(miAv,miCa)
    LiberarEsquina(10,10)
  fin
areas
  a1: AreaP(1,1,6,6)
  a2: AreaP(7,1,12,6)
  a3: AreaP(13,1,18,6)
  ac: AreaP(20,1,20,1)
  perdedores: AreaPC(10,10,10,10)
robots
  robot trabajador
  variables
    flores,id:numero
    sigo,gane:boolean
  comenzar
    flores:=0
    RecibirMensaje(id,coord)
    repetir 4
      lado(flores)
      EnviarMensaje(V,coord)
      RecibirMensaje(sigo,coord)
    EnviarMensaje(id, coord)
    EnviarMensaje(flores,coord)
    RecibirMensaje(gane,coord)
    si (gane = F)
      repetir flores
        depositarFlores
  fin
  robot coordinador
  variables
    ter:boolean
    ganador,id,maxF,f:numero
  comenzar
    maxF:=-1
    identificar
    repetir 4
      repetir 3
        RecibirMensaje(ter, *)
      continuar
    repetir 3
      RecibirMensaje(id, *)
      si (id=1)
        RecibirMensaje(f, r1)
      sino
        si(id=2)
          RecibirMensaje(f,r2)
        sino
          RecibirMensaje(f,r3)
      maximo(id,f,ganador,maxF)
    avisarGanador(ganador)
    Informar (ganador)
  fin
variables
  r1,r2,r3:trabajador
  coord:coordinador
comenzar
  AsignarArea(r1,a1)
  AsignarArea(r2,a2)
  AsignarArea(r3,a3)
  AsignarArea(r1,perdedores)
  AsignarArea(r2,perdedores)
  AsignarArea(r3,perdedores)
  AsignarArea(coord,ac)
  Iniciar(r1,1,1)
  Iniciar(r2,7,1)
  Iniciar(r3,13,1)
  Iniciar(coord,20,1)
fin